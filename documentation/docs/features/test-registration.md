---
tags: [Начало]
---

# Регистрация тестовых методов (ЮТТесты)

Кроме того, чтобы написать тестовые сценарии, разработчик должен зарегистрировать их в движке.
Регистрация выполняется внутри предопределенного экспортного метод `ИсполняемыеСценарии`, который обязательно должен находиться в тестовом модуле.

```bsl

Процедура ИсполняемыеСценарии() Экспорт
    
    // Регистрация тестов
    ЮТТесты // Регистрация тестов выполняет через модуль регистратор
        .ДобавитьТестовыйНабор("Математические методы")               // Набор - объединение тестов
            .ДобавитьТест("Сложение")                                 // Обязательно при регистрации указываем имя экспортного метода
            .ДобавитьТест("Вычитание", "Вычитание")                   // Также можно указать представление теста
            .ДобавитьТест("Вычитание", "ВычитаниеСервер", , "Сервер") // Контекст исполнения, по умолчанию тест выполняется во всех контекстах модуля
            .ДобавитьКлиентскийТест("УмножениеНаКлиенте")                   // Есть отдельный метод для регистрации клиентских тестов
            .ДобавитьСерверныйТест("ДелениеНаСервере", "Деление на сервер") // Есть отдельный метод для регистрации серверных тестов
        .ДобавитьТестовыйНабор("Строковые методы")
            .ДобавитьТест("СтрНайти")
            .ДобавитьТест("СтрРазделить");

КонецПроцедуры

```

Этот метод автоматически вызывается тестовым движком при старте тестирования.

Как видно из примера выше, регистрация выполняется с помощью методов модуля `ЮТТесты`.

Он позволяет:

* Объединять тесты в наборы, указывать теги, по которым возможно формировать отборы запускаемых тестов
* Указывать контекст вызова, например, для клиент-серверного модуля (или метода) можно реализовать тесты в одном тестовом модуле, проверяющие логику и на клиенте, и на сервере.
* Указывать параметры выполнения тестов.
* Организовывать черновики, регистрируя нереализованные тесты.
